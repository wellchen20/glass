apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'

int appVersionCode
String appVersionName

File propFile = file('../scripts/info.dat');
if (propFile.exists()) {
    Properties props = new Properties()
    props.load(new FileInputStream(propFile))
    String major = props.getProperty('major')
    String minor = props.getProperty('minor')
    String revision = props.getProperty('revision')
    appVersionName = major + '.' + minor + '.' + revision
    appVersionCode = Integer.parseInt(major) * 10000 + Integer.parseInt(minor) * 100 + Integer.parseInt(revision)
} else {
    appVersionCode = 1
    appVersionName = "1.0"
}

android {
    compileSdkVersion sdk.compileSdkVersion
    defaultConfig {
        applicationId "com.xylink.sdk.sample"
        minSdkVersion sdk.minSdkVersion
        targetSdkVersion sdk.targetSdkVersion
        versionCode appVersionCode
        versionName appVersionName
        ndk {
            // 设置支持的SO库架构
            abiFilters 'armeabi-v7a'
        }
    }

    greendao {
        //版本号，升级时可配置
        schemaVersion 1
        daoPackage 'com.xylink.sdk.sample.gen'
        targetGenDir 'src/main/java'
    }


    signingConfigs {
        release {
            keyAlias 'xylink_sdk'
            keyPassword 'xylink'
            storeFile file('./SDKSign')
            storePassword 'xylink'
        }
        debug {
            keyAlias 'xylink_sdk'
            keyPassword 'xylink'
            storeFile file('./SDKSign')
            storePassword 'xylink'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    applicationVariants.all {
        variant ->
            variant.outputs.all {
                outputFileName = "xylinksdk_" + variant.name + "-" + versionName + ".apk"
            }
    }

    compileOptions {
        sourceCompatibility jdk.sourceCompatibility
        targetCompatibility jdk.targetCompatibility
    }
}
// 引入aar, 需加入这个声明
repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    implementation libs.supportV7
    implementation libs.constraintLayout
    implementation libs.retrofit2
    implementation libs.rxBinding2
    implementation libs.rxPermission2

    // replace with the aar name (替换成最新的aar)
    implementation(name: 'xylink-sdk-release-2.29.2-829', ext: 'aar') {
        transitive = false
    }
    implementation(name: 'libuvccamera-release', ext: 'aar')

    implementation project(':uikit')

    // picture selector, you may not need this, can replace with yours
    implementation libs.matisse
    implementation libs.glide
    // uvc USB外接摄像头必须 不需要的可删除
//        implementation libs.serenegiantCommon

    // 必须, SDK需要依赖这些库, 库版本尽量保持一致, 否则有冲突自行解决
    implementation libs.rxJava2
    implementation libs.rxAndroid2
    implementation libs.gson
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    implementation 'com.rokid.ai.glass:instructsdk:1.5.6'
    implementation 'org.greenrobot:greendao:3.2.2'
}
